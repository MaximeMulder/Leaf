Language leaf;

Lexer

	digit = '0' .. '9';
	letter = ('A' .. 'Z') | ('a' .. 'z') | '_';

	number = digit+;
	string = Shortest '"' (Any - endline)* '"';
	identifier = letter+;

	whitespace = #9 | ' ';
	endline = #13 | #10;
	comment_block = Shortest '/*' Any* '*/';
	comment_line = Shortest '//' (Any - endline)*;

	operator_binary1 = '*' | '/';
	operator_binary2 = '+' | '-';
	operator_binary3 = '<' | '>' | '<=' | '>=';
	operator_binary4 = '==' | '!=';
	operator_binary5 = '&&';
	operator_binary6 = '||';

	Ignored
		whitespace, endline, comment_block, comment_line;

Parser

	file = statements;

	// Statements

	statements
		= {some:} statements statement
		| {none:}
		;

	statement = expression ';';

	// Expressions

	expression
		= {class:}    class
		| {function:} function
		| {block:}    block

		| {if:} 'if' [condition:] expression [body:] block else
		| {loop:} 'loop' [body:] expression
		| {while:}       'while' [condition:] expression 'do' [body:] expression
		| {while_block:} 'while' [condition:] expression      [body:] block
		| {do_while:} 'do' [body:] expression 'while' [condition:] expression
		| {for:}       'for' [element:] variable 'in' [array:] expression 'do' [body:] expression
		| {for_block:} 'for' [element:] variable 'in' [array:] expression      [body:] block

		| {declaration:} 'let' identifier
		| {declaration_type:} 'let' identifier ':' [type:] expression

		| {return:}   'return'   expression
		| {break:}    'break'    expression
		| {continue:} 'continue' expression

		| {null:}  'null'
		| {true:}  'true' 
		| {false:} 'false'

		| {number:}   [content:] number
		| {string:}   [content:] string
		| {variable:} [content:] identifier

		| {group:} '(' expression ')'
		| {array:} '[' arguments  ']'

		| {call:}   expression '(' arguments ')'
		| {access:} expression '[' arguments ']'

		| {chain:} expression '.' identifier
		| {assignment:} [reference:] expression '=' expression

		| {operation_pre1:} [operator:] '&' expression
		| {operation_pre2:} [operator:] '*' expression
		| {operation_post1:} expression [operator:] '&' 
		| {operation_post2:} expression [operator:] '?'

		| {operation_binary1:} [left:] expression [operator:] operator_binary1 [right:] expression
		| {operation_binary2:} [left:] expression [operator:] operator_binary2 [right:] expression
		| {operation_binary3:} [left:] expression [operator:] operator_binary3 [right:] expression
		| {operation_binary4:} [left:] expression [operator:] operator_binary4 [right:] expression
		| {operation_binary5:} [left:] expression [operator:] operator_binary5 [right:] expression
		| {operation_binary6:} [left:] expression [operator:] operator_binary6 [right:] expression
		;

	Priority
		Left loop, while, do_while, for, access, call, chain;
		Left operation_pre1, operation_pre2;
		Left operation_post1, operation_post2;
		Left operation_binary1;
		Left operation_binary2;
		Left operation_binary3;
		Left operation_binary4;
		Left operation_binary5;
		Left operation_binary6;
		Left return, break, continue;
		Left declaration_type;
		Left assignment;

	else
		= {some:} 'else' [body:] block
		| {none:}
		;

	block = '{' statements '}';

	arguments
		= {some:} argument arguments_more
		| {none:}
		;

	arguments_more
		= {some:} arguments_more ',' argument
		| {none:}
		;

	argument = expression;

	// Functions

	function = 'function' name '(' parameters ')' type block;

	parameters
		= {some:} parameter parameters_more
		| {none:}
		;

	parameters_more
		= {some:} parameters_more ',' parameter
		| {none:}
		;

	parameter = variable;

	// Classes

	class = 'class' name [parent:] type '{' members '}';

	members
		= {some:} members visibility member ';'
		| {none:}
		;

	visibility
		= {public:}    'public'
		| {protected:} 'protected'
		| {private:}   'private'
		| {default:}
		; 

	member
		= {method:} function
		| {class:} class
		| {attribute:} identifier
		;

	// Miscellaneous

	name
		= {some:} identifier
		| {none:}
		;

	variable = identifier type;

	type
		= {some:} ':' expression
		| {none:}
		;
